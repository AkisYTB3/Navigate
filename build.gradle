plugins {
    id 'com.gradleup.shadow' version '8.3.0'
    id 'java'
    id 'xyz.jpenilla.run-paper' version '2.3.1'
}

group = project.group
version = project.version

repositories {
    mavenCentral()
    maven { url 'https://repo.papermc.io/repository/maven-public/' }
    maven { url 'https://repo.aikar.co/content/groups/aikar/' }
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:${paperapi}")
    implementation("co.aikar:acf-paper:${paperacf}")
}

tasks {
    runServer {
        minecraftVersion("1.21.4")
        jvmArgs("-Dcom.mojang.eula.agree=true")
    }

    shadowJar {
        relocate("co.aikar.commands", "org.notionsmp.navigate.lib.commands")
        relocate("co.aikar.locales", "org.notionsmp.navigate.lib.locales")

        archiveClassifier.set("")
    }

    jar { enabled = false }
    build { dependsOn(shadowJar) }
}
def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('paper-plugin.yml') {
        expand props
    }
}
